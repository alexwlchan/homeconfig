#!/usr/bin/osascript

-- Convert an Acorn file to another format (for example, PNG or JPEG)
--
-- Takes two paths as arguments: the original path and the export path.
-- For example:
--
--    export_acorn_file ~/Desktop/fire.acorn ~/Desktop/fire.png
--
-- will create a new PNG image.
--
-- The script tries to be well-behaved about your windows: if the Acorn file
-- is already open for editing, it uses that, otherwise it opens the file
-- and then closes it afterward.

on run argv
  if (count of argv) is not 2
    log "Usage: export_acorn_file <ORIGINAL_PATH> <EXPORT_PATH>"
    error number 1
  end if

  set ORIGINAL_PATH to (item 1 of argv)
  set EXPORT_PATH to (item 2 of argv)

  tell application "Acorn"
    repeat with theWindow in every window
      if document of theWindow is not missing value then
        set filePath to (file of the document of theWindow)
        if (POSIX path of filePath) = ORIGINAL_PATH then
          save document of theWindow in EXPORT_PATH
          return
        end if
      end if
    end repeat

    set theDocument to open ORIGINAL_PATH
    save theDocument in EXPORT_PATH
    close theDocument
  end tell
end run
